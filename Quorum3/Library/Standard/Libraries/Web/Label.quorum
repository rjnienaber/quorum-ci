package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
use Libraries.Web.FormAttributeAccepter
use Libraries.Web.MouseAttributeAccepter

/*
The Label class represents HTML's (Hypertext Markup Language) label tag which 
specifies labeling text for an input tag. This label can be bound to an input 
tag by either setting the for attribute to the label id or by placing the input 
tag inside the label tag.
You can find more information about this tag at: 
    <a href="http://www.w3schools.com/tags/tag_label.asp">The label attribute</a>.


Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      Form form
      form:SetName("myForm")

      Label label
      label:SetElementId("idOfInput")
      label:SetDescription("I'm a label")

      Input formInput
      formInput:SetIdentifier("idOfInput")

      form:Add(label)
      form:Add(formInput)

      page:AddToBody(form)
      output page:Generate()
   end
end
*/
class Label is WebTag, FormAttributeAccepter, MouseAttributeAccepter
    constant text elementId = "for"
    constant text formId = "form"

    /*
        This action gets the element id attribute of the label. The element id
        allows the label to be associated with an input element.

        Attribute: Returns The element id of the corresponding input element. 

        Attribute: Example
        use Libraries.Web.Label
        use Libraries.Web.Attribute
        Label label
        Attribute id = label:GetElementId()
    */
    action GetElementId returns Attribute
        return me:GetAttribute(elementId)
    end

     /*
        This action sets the element id attribute of the label. The element id
        allows the label to be associated with an input element.

        Attribute: Parameter value The element id that the label is associated with. 

        Attribute: Example
        use Libraries.Web.Label
        use Libraries.Web.Input

        Label label
        label:SetElementId("male")
        
    */
    action SetElementId(text value)
        AddAttribute(elementId, value)
    end

    /*
        This action gets the form id attribute of the label. The form id
        allows the label to be associated with a form element.

        Attribute: Returns The element id of the corresponding input element. 

        Attribute: Example
        use Libraries.Web.Label
        use Libraries.Web.Attribute
        Label label
        Attribute id = label:GetFormId()
    */
    action GetFormId returns Attribute
        return  me:GetAttribute(formId)
    end

    /*
        This action sets the form id attribute of the label. The form id
        allows the label to be associated with a form element.

        Attribute: Parameter value The form id that the label is associated with. 

        Attribute: Example
        use Libraries.Web.Form
        use Libraries.Web.Label
        Form form
        form:SetName("form1")
        Label label
        label:SetFormId("form1")
        
    */
    action SetFormId(text value)
        AddAttribute(formId, value)
    end


    action Generate returns text
        text result = "<label " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</label>"
        return result
    end
end