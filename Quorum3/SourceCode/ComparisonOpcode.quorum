package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Translate.JavaBytecodeOpcodes

class ComparisonOpcode is QuorumOpcode 
    public QuorumOpcode left = undefined
    public QuorumOpcode right = undefined
    public integer opcode = -1
    public JavaBytecodeMethodWriter methodWriter = undefined
    JavaBytecodeOpcodes opcodes
    public boolean isTextCompare = false
    public integer textCompareValue = -2

    action Write
        JavaBytecodeLabel jumpNotEqual
        JavaBytecodeLabel jumpPastLabel
        
        left:Write()
        WriteLeft()
        right:Write()
        WriteRight()

        if isTextCompare
            methodWriter:VisitMethodInvoke(opcodes:GetInvokeVirtual(), "java/lang/String", "compareTo", "(Ljava/lang/String;)I", false)
        end
        
        methodWriter:VisitJump(opcode, jumpNotEqual)
        methodWriter:VisitConstant(1)
        methodWriter:VisitJump(opcodes:GetGoto(), jumpPastLabel)
        methodWriter:VisitLabel(jumpNotEqual)
        methodWriter:VisitConstant(0)
        methodWriter:VisitLabel(jumpPastLabel)
        
    end

    action WriteLeft
    end

    action WriteRight
    end
    
    action GetTemplateCopy returns QuorumOpcode
        ComparisonOpcode op
        op:opcode = opcode
        op:isTextCompare = isTextCompare
        op:textCompareValue = textCompareValue
        return op
    end
end