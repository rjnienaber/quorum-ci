package Libraries.Language.Compile.Test

use Libraries.Language.Compile.all
use Libraries.Containers.HashTable
use Libraries.Containers.Array
use Libraries.Language.Compile.Parser
use Libraries.System.File
use Libraries.Containers.Blueprints.Iterator
use Libraries.Language.Compile.Translate.QuorumJarGenerator
use Libraries.System.StackTraceItem
use Libraries.Compute.Math
use Libraries.System.DateTime

class CompilerTestSuite
    integer total = 0
    integer passed = 0
    integer failed = 0
    Math math
    ExpressionsTester expressions

    on create
        expressions:SetCompilerTestSuite(me)
    end

    action Main
        DateTime date
        start = date:GetEpochTime()
        output "Starting Quorum 3.0 Test Suite" //+ compiler:VERSION + "."
        output ""
        output "List of Tests:"
        output expressions:GetName()
        expressions:Test()
        output ""

        DateTime date2
        finish = date2:GetEpochTime()
        number totalTime = finish - start
        totalTime = totalTime / 1000.0
        output "Finished Testing in " + totalTime + " seconds."
        output "Tests Passed: " + passed + " out of " + total

        number percent = math:Round((passed  * 100.0) / (total * 1.0), 3) //known bug workaround. This value should be 2, not three
        output "Total Percentage: " + percent + "%"

    end

    action RunTest(text source) returns CompilerTestResult
        CompilerTestResult result = undefined
        Compiler compiler
        File file
        file:SetPath(source)
        compiler:SetMain(file)

        Parser parser
        compiler:Parse(file, parser)
        TypeCheckListener checker
        compiler:Parse(file, checker)
        QuorumBytecodeListener bytecode
        compiler:Parse(file, bytecode)
        
        CompilerErrorManager manager = parser:GetCompilerErrorManager()

        if manager:IsCompilationErrorFree() //run the program
            QuorumJarGenerator generator

            File jar = compiler:GetDistributionFile()
            generator:SetCompiler(compiler)
            generator:Write()
            result = RunClassFile(jar)
        else
            CompilerTestResult result2
            result = result2
        end
        result:name = source
        return result
    end

    action RunTest(text source, text reply)
        CompilerTestResult result = undefined
        Compiler compiler
        File file
        file:SetPath(source)
        compiler:SetMain(file)

        Parser parser
        compiler:Parse(file, parser)
        TypeCheckListener checker
        compiler:Parse(file, checker)
        QuorumBytecodeListener bytecode
        compiler:Parse(file, bytecode)
        
        CompilerErrorManager manager = parser:GetCompilerErrorManager()

        if manager:IsCompilationErrorFree() //run the program
            QuorumJarGenerator generator

            File jar = compiler:GetDistributionFile()
            generator:SetCompiler(compiler)
            generator:Write()
            result = RunClassFile(jar)
            
            Array<text> results = result:lines
            if not results:IsEmpty()
                line = results:Get(0)
                if line = reply //we passed!
                    result:passed = true
                end
            end
        else
            CompilerTestResult result2
            result = result2
        end

        result:name = source
        Add(result)
    end

    action RunTest(text source, text reply1, text reply2)
        CompilerTestResult result
        result:name = source
        check
            result = RunTest(result:name)
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 2
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2
                       result:passed = true
                    end
                end
            end
            
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        Add(result)
    end

    action RunTest(text source, text reply1, text reply2, text reply3)
        CompilerTestResult result
        result:name = source
        check
            result = RunTest(result:name)
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 3
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3
                       result:passed = true
                    end
                end
            end
            
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        Add(result)
    end

    action RunTest(text source, text reply1, text reply2, text reply3, text reply4)
        CompilerTestResult result
        result:name = source
        check
            result = RunTest(result:name)
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 2
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3 and
                       lines:Get(3) = reply4
                       result:passed = true
                    end
                end
            end
            
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        Add(result)
    end

    action Add(CompilerTestResult result)
        if result = undefined
            failed = failed + 1
        elseif result:passed
            passed = passed + 1
        else
            failed = failed + 1
        end
        total = total + 1
        text value = ""

        if result:exceptionThrown
            value = result:name + " ... " + "failed"
        elseif not result:exceptionThrown and result:passed = false

            text error = result:errorMessage
            value = result:name + " ... " + "failed: exception thrown with message: " + error
            Array<StackTraceItem> stack = result:stackTrace
            if stack not= undefined
                if not stack:IsEmpty()
                    StackTraceItem item = stack:Get(0)
                    value = value + ", " + item:GetClassName() + ":" + item:GetMethodName() + ":" + item:GetLineNumber()
                end
            end
        else
            value = result:name + " ... " + "passed"
        end
        
        output value
    end

    system action RunClassFile(File file) returns CompilerTestResult
end