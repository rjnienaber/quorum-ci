package Libraries.Language.Compile

use Libraries.Containers.Blueprints.Iterator
use Libraries.Containers.HashTable
use Libraries.Containers.List

class CompilerErrorIterator<T> is Iterator<T>
    HashTable<text, List<CompilerError>> errorsPerFile
    Iterator<List<CompilerError>> files = undefined
    Iterator<CompilerError> currentFile = undefined
    CompilerError error = undefined

    action GetErrors returns HashTable<text, List<CompilerError>>
        return me:errorsPerFile
    end

    action SetErrors(HashTable<text, List<CompilerError>> errors)
        me:errorsPerFile = errors

        Rewind()
        
    end

    action HasNext returns boolean
        if files = undefined or currentFile = undefined
            return false
        end
        
        if currentFile:HasNext()
            return true
        else 
            if not files:HasNext()
                return false
            end
            List<CompilerError> list = files:Next()
            currentFile = list:GetIterator()
            if currentFile:HasNext()
                return true
            end
        end

        return false
    end

    action Next returns T
        if files = undefined or currentFile = undefined
            return undefined
        end

        if currentFile not= undefined
            if currentFile:HasNext()
                error = currentFile:Next()
                return error
            else //the current file is done. is there another file?
                if files:HasNext()
                    List<CompilerError> list = files:Next()
                    currentFile = list:GetIterator()
                    error = currentFile:Next()
                    return error
                else 
                    currentFile = undefined
                    files = undefined
                end
            end
        end
        return undefined
    end

    action GetCurrent returns T
        if error = undefined
            return Next()
        else 
            return error
        end
    end

    action Rewind
        files = errorsPerFile:GetValueIterator()
        if files not= undefined
            if files:HasNext()
                List<CompilerError> errors = files:Next()
                if errors not= undefined
                    currentFile = errors:GetIterator()
                end
            end
        end
    end
end