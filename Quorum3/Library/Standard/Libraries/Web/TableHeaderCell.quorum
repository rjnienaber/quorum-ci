package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes

/*
The TableHeaderCell class represents HTML's (Hypertext Markup Language) th tag 
which is a header cell table element often contained in a TableRow. 
A TableHeader object represents a header and singular cell in a table.
You can find more information about this tag at:
<a href="http://www.w3schools.com/tags/tag_th.asp">The th attribute</a>.

Attribute: Example

use Libraries.Web.all
class Main
   action main
      //make a web page
      WebPage page
      WebTable table
      //add a TableRow
      TableRow row
      TableHeader cell
      cell:SetDescription("Name")
      row:Add(cell)
      table:AddRow(row)
      page:AddToBody(table)
   end
end
*/
class TableHeaderCell is WebTag
    constant text headers = "headers"
    constant text columnSpan = "colspan"
    constant text rowSpan = "rowspan"
    constant text scope = "scope"

    /*
        This action gets the number of columns a cell spans in the web table. the
        default span is 1. 

        Attribute: Returns The number of columns a cell spans. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell

        TableHeaderCell th
        integer span = th:GetColumnSpan()
    */
    action GetColumnSpan returns integer
        Attribute colSpan = GetAttribute(me:columnSpan)
        if colSpan = undefined
            return 1
        else
            return cast(integer,colSpan:GetValue())
        end
    end

    /*
        This action sets the column span for a cell in a table. The column span
        should be a positive integer. In some browsers a span of 0 means to span
        all columns to the end of the group.

        Attribute: Parameter value The number of columns a cell should span. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell

        TableHeaderCell th
        th:SetColumnSpan(2)
        
    */
    action SetColumnSpan(integer value)
        AddAttribute(columnSpan, value)
    end

    /*
        This action gets the number of rows a cell spans in the web table. the
        default span is 1. 

        Attribute: Returns The number of rows a cell spans. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell

        TableHeaderCell th
        integer rowSpan = th:GetRowSpan()
    */
    action GetRowSpan returns integer
        Attribute rowSpan = GetAttribute(me:rowSpan)
        if rowSpan = undefined
            return 1
        else
            return cast(integer,rowSpan:GetValue())
        end
    end

    /*
        This action sets the row span for a cell in a table. The column span
        should be a positive integer. In some browsers a span of 0 means to span
        all rows to the end of the table section.

        Attribute: Parameter value The number of rows a cell should span. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell

        TableHeaderCell th
        th:SetRowSpan(2)
        
    */
    action SetRowSpan(integer value)
        AddAttribute(rowSpan, value)
    end

    /*
        This action gets the header id of a header in a table.

        Attribute: Returns The header id. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell
        use Libraries.Web.Attribute

        TableHeaderCell th
        Attribute headerID = th:GetHeaders()
    */
    action GetHeaders returns Attribute
        return me:GetAttribute(headers)
    end

    /*
        This action sets the header id that links this header to a cell.

        Attribute: Parameter value The header id. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell

        //generate a header with an id of name
        TableHeaderCell th
        th:SetHeaders("name")

        //link the cell to the header by its id
        TableHeaderCell td
        td:SetHeaders("name")
        
    */
    action SetHeaders(text value)
        AddAttribute(headers, value)
    end

    /*
        This action gets the scope of the header. The scope specifies if the
        header is for a row, column, group of rows, or group of columns.

        Attribute: Returns The scope attribute. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell
        use Libraries.Web.Attribute

        TableHeaderCell th
        Attribute scope = th:GetHeaders()
    */
    action GetScope returns Attribute
        return me:GetAttribute(scope)
    end

    /*
        This action sets the scope of the header. The scope specifies if the
        header is for a row("row"), column("col"), group of rows("rowgroup"), or group of columns("colgroup").

        Attribute: Parameter value The scope of this header. 

        Attribute: Example
        use Libraries.Web.TableHeaderCell

        TableHeaderCell th
        th:SetScope("colgroup")
    */
    action SetScope(text value)
        AddAttribute(scope, value)
    end

    
    action Generate returns text
        text result = "<th " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</th>"
        return result
    end

    

end