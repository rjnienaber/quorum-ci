package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Symbol.ActionCallResolution
use Libraries.Language.Compile.Symbol.Action
use Libraries.Language.Compile.Symbol.Class
use Libraries.Language.Compile.Symbol.Variable
use Libraries.Containers.Array

class ActionCallOpcode is QuorumOpcode
    JavaBytecodeOpcodes opcodes
    boolean isActionCall = false
    ActionCallResolution actionResolution = undefined
    boolean pushOnMe = false
    boolean isField = false
    Variable field = undefined
    Class fieldHolder = undefined
    Array<QuorumOpcode> parameters = undefined

    action GetParameters returns Array<QuorumOpcode>
        return parameters
    end

    action SetParameters(Array<QuorumOpcode> array)
        parameters = array
    end

    action IsField returns boolean
        return isField
    end

    action SetIsField(boolean field)
        isField = field
    end

    action GetField returns Variable
        return field
    end

    action SetField(Variable field)
        me:field = field
    end

    action GetFieldHolder returns Class
        return fieldHolder
    end

    action SetFieldHolder(Class fieldHolder)
        me:fieldHolder = fieldHolder
    end

    action GetPushOnMe returns boolean
        return pushOnMe
    end

    action SetPushOnMe(boolean this)
        pushOnMe = this
    end


    action SetMethodWriterToAllChildren(JavaBytecodeMethodWriter writer)
        SetMethodWriter(writer)
        integer numParams = parameters:GetSize()
        i = 0
        repeat numParams times
            QuorumOpcode param = parameters:Get(i)
            param:SetMethodWriterToAllChildren(writer)
            i = i + 1
        end
    end

    action GetActionCallResolution returns ActionCallResolution
        return actionResolution
    end

    action SetActionCallResolution(ActionCallResolution resolution)
        actionResolution = resolution
        Action act = actionResolution:resolvedAction
        Type returnType = act:GetReturnType()
        me:SetType(returnType)
    end

    action IsActionCall returns boolean
        return isActionCall
    end

    action SetIsActionCall(boolean act)
        isActionCall = act
    end

    action Write //TODO: make this work for action calls and fields
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        if isActionCall
            //if there's a variable, load it.
            //this isn't right, but load "THIS" onto the stack for testing
            if pushOnMe
                methodWriter:VisitVariable(opcodes:GetObjectLoad(), 0)
                //push on the variable $hidden
            end

            //actual plan - push on variables, if necessary

            //push on parameters, if necessary
            integer numParams = parameters:GetSize()
            i = 0
            repeat numParams times
                QuorumOpcode param = parameters:Get(i)
                param:Write()
                i = i + 1
            end

            Action act = actionResolution:resolvedAction
            Class clazz = act:GetParentClass()
            if act not= undefined and clazz not= undefined
                methodWriter:VisitMethodInvoke(opcodes:GetInvokeInterface(), 
                    clazz:ConvertStaticKeyToBytecodeInterfacePath(), act:GetName(), 
                    act:ConvertActionToBytecodeParameterInterfaceSignature(), true)
            end
        else
            Type type = GetType()
            if not field:IsField()
                methodWriter:VisitVariable(opcodes:ConvertTypeToBytecodeLoadOpcode(GetType()), field:GetBytecodeLocation())
            else
                //Now write the getter for the field
                path = fieldHolder:ConvertStaticKeyToBytecodeInterfacePath()
                getterName = fieldHolder:ConvertToActionNameFromField(field, true)
                methodName = type:ConvertToSignatureFromField(true)
                methodWriter:VisitVariable(opcodes:GetObjectLoad(), 0)
                methodWriter:VisitMethodInvoke(opcodes:GetInvokeInterface(), 
                    path, getterName, methodName, true)
            end
        end
    end

    action WriteJavaScript returns text 
        text result = ""
        if isActionCall
            Action act = actionResolution:resolvedAction
            Class clazz = act:GetParentClass()
        else
            result = result + field:GetName()
        end
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        ActionCallOpcode op
        Type t = GetType()
        me:SetType(t:Copy())
        return op
    end
end