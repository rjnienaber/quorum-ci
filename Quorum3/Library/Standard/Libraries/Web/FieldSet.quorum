 package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The FieldSet class represents HTML's (Hypertext Markup Language) fieldset tag 
which allows elements in a form to be grouped inside of a box.
You can find more information about this tag at: 
    <a href="http://www.w3schools.com/tags/tag_fieldset.asp">The fieldset attribute</a>.


Attribute: Example

use Libraries.Web.all
class Main
   action main
      //make a web page
      WebPage page
      
      //create a form
      Form form
      //create a fieldset
      FieldSet field
      //create a column
      Input input
      //setup that input
      input:SetType("text")
      //add the input to the fieldset
      field:Add(input)
      //add the fieldset to the form
      form:Add(field)
      page:AddToBody(form)
   end
end
*/
class FieldSet is WebTag

    constant text disabled = "disabled"
    constant text formId = "formid"
    constant text name = "name"

    /*
        This action gets whether the group of form elements is disabled or not. 
        True is returned if the elements are disabled and false is returned if 
        they are enabled.

        Attribute: Returns True when disabled and false when enabled. 

        Attribute: Example
        use Libraries.Web.FieldSet
        FieldSet field
        boolean isDisabled = field:GetDisabled()
    */
    action GetDisabled returns boolean
        Attribute disable = GetAttribute(me:disabled)
        return disable:GetValue() = me:disabled
    end
    
    /*
        This action sets the elements in the fieldset to be disabled if true or
        enabled if false.

        Attribute: Parameter value True to disable and false to enable.

        Attribute: Example
        use Libraries.Web.FieldSet
        FieldSet field
        field:SetDisabled(true)
        
    */
    action SetDisabled (boolean value)
        if value
            AddAttribute(disabled, disabled)
        else
            RemoveAttribute(disabled)
        end
    end

    /*
        This action gets the form id which links the fieldset to one or more
        forms.

        Attribute: Returns The form id attribute. 

        Attribute: Example
        use Libraries.Web.FieldSet
        use Libraries.Web.Attribute
        FieldSet field
        Attribute fID = field:GetFormId()
        if fID not= undefined
            text id = fID:GetValue()
        end
    */
    action GetFormId returns Attribute
        return GetAttribute(formId)
    end
    
    /*
        This action sets the form id of the field set and links the fieldset
        to a form.

        Attribute: Parameter value The form id.

        Attribute: Example
        use Libraries.Web.FieldSet
        FieldSet field
        field:SetFormId("personalInfo")
        
    */
    action SetFormId(text value)
        AddAttribute(formId, value)
    end

    /*
        This action gets the name of the fieldset.

        Attribute: Returns The fieldsets name. 

        Attribute: Example
        use Libraries.Web.FieldSet
        use Libraries.Web.Attribute
        FieldSet field
        Attribute fName = field:GetName()
        if fName not= undefined
            text name = fName:GetValue()
        end
    */
    action GetName returns Attribute
        return GetAttribute(name)
    end
    
    /*
        This action sets the name of the fieldset element.

        Attribute: Parameter value The name of the fieldset.

        Attribute: Example
        use Libraries.Web.FieldSet
        FieldSet field
        field:SetName("personal")
        
    */
    action SetName(text value)
        AddAttribute(name, value)
    end
    
    action Generate returns text
        text result = "<fieldset " 
        result = result + GenerateAttributes() + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</fieldset>"
        return result
    end
end