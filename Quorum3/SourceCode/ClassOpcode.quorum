package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator
use Libraries.Language.Compile.Symbol.Action
use Libraries.Language.Compile.Translate.JavaBytecodeOpcodes
use Libraries.Language.Compile.Symbol.Class
use Libraries.Containers.Array
use Libraries.System.File
use Libraries.Language.Compile.QualifiedName

class ClassOpcode is QuorumOpcode
    QualifiedName packageValue = undefined
    Class clazz = undefined
    JavaBytecodeMethodWriter methodWriter = undefined
    ActionOpcode constructor = undefined
    List<ActionOpcode> actions
    List<QuorumOpcode> fields

    JavaBytecodeClassWriter classWriter = undefined
    JavaBytecodeClassWriter interfaceWriter = undefined
    QuorumBytecodeConverter converter
    JavaBytecodeOpcodes opcodes
    text name = ""
    text staticKey = ""
    constant text JAVA_THROWABLE = "java/lang/Throwable"
    constant text JAVA_OBJECT = "java/lang/Object"
    constant text QUORUM_ERROR = "quorum/Libraries/Language/Errors/Error"
    constant integer ME = 0
    constant text QUORUM = "quorum"
    text buildPath = ""
    text runPath = ""
    File buildFile = undefined
    File buildInterfaceFile = undefined

    

    action GetBuildFile returns File
        return buildFile
    end

    action GetBuildInterfaceFile returns File
        return buildInterfaceFile
    end

    action GetBuildPath returns text
        return buildPath
    end

    action GetRunPath returns text
        return runPath
    end

    action SetBuildPath(text build)
        buildPath = build
    end

    action SetRunPath(text build)
        runPath = build
    end

    action GetStaticKey returns text
        return staticKey
    end

    action SetStaticKey(text staticKey)
        me:staticKey = staticKey
    end

    action GetPackage returns QualifiedName
        return packageValue
    end

    action SetPackage(QualifiedName value)
        packageValue = value
    end

    action GetName returns text
        return name
    end

    action SetName(text name)
        me:name = name
    end

    action GetClassWriter returns JavaBytecodeClassWriter
        return classWriter
    end

    action GetInterfaceWriter returns JavaBytecodeClassWriter
        return interfaceWriter
    end

    action SetClassWriter(JavaBytecodeClassWriter writer)
        classWriter = writer
    end

    action SetInterfaceWriter(JavaBytecodeClassWriter writer)
        interfaceWriter = writer
    end

    action GetClass returns Class
        return clazz
    end

    action SetClass(Class clazz)
        me:clazz = clazz
    end

    action Add(ActionOpcode method)
        actions:Add(method)
    end

    action AddField(QuorumOpcode fieldOpcode) 
        fields:Add(fieldOpcode)
    end

    action SetConstructor(ActionOpcode method)
        constructor = method
    end

    action Write
        integer version = opcodes:GetJavaVersion(7)
        integer access = opcodes:GetPublic() + opcodes:GetSuper()
        text interfaceName = converter:ConvertClassNameToInterfaceName(name)
        Array<text> interfaces
        //interfaces:Add(interfaceName)
        text null = undefined //temporary workaround for Bug#97 https://quorum.atlassian.net/browse/QUOR-97
        //once fully integrated, check if this is a the Quorum error class or a subclass. For now, just weave in Object.
        classWriter:Visit(version, access, converter:ConvertStaticKeyToBytecodePath(staticKey), null, JAVA_OBJECT, interfaces)

        Iterator<QuorumOpcode> fieldIterator = fields:GetIterator()
        repeat while fieldIterator:HasNext()
            QuorumOpcode field = fieldIterator:Next()
            field:Write()
        end

        ComputeConstructor(true)
        ComputeConstructor(false)

        //does this class have a main method?
        if clazz:HasMainAction()
            WriteMain()
        end


        Iterator<ActionOpcode> actionIterator = actions:GetIterator()
        repeat while actionIterator:HasNext()
            ActionOpcode act = actionIterator:Next()
            act:Write()
        end

        classWriter:VisitEnd()

        File classFile
        text path = buildPath + "/" + QUORUM + "/" + packageValue:GetPath() 
        classFile:SetPath(path)
        classFile:CreateDirectories()
        classFile:SetPath(path + "/" + clazz:GetName() + ".class")
        classWriter:Write(classFile)

        buildFile = classFile

        interfaceWriter:VisitEnd()
        File interfaceFile
        path = buildPath + "/" + QUORUM + "/" + packageValue:GetPath()

        interfaceFile:SetPath(path + "/" + clazz:GetName() + "$Interface.class")
        interfaceWriter:Write(interfaceFile)

        buildInterfaceFile = interfaceFile
    end

    action WriteMain
        //make a new method, if it exists. 
        text null = undefined
        methodWriter = 
        classWriter:VisitMethod(opcodes:GetPublic() + opcodes:GetStatic(), 
            "main", "([Ljava/lang/String;)V", null, undefined)

        text key = clazz:GetStaticKey()
        text bytecode = converter:ConvertStaticKeyToBytecodePath(key)

        //create the new object and place it on the stack
        methodWriter:VisitType(opcodes:GetNew(), bytecode)
        methodWriter:VisitInstruction(opcodes:GetDuplicate())
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeSpecial(), bytecode, "<init>", "()V", false)

        //store it in a variable and load it back on the stack
        methodWriter:VisitVariable(opcodes:GetObjectStore(), 1)
        methodWriter:VisitVariable(opcodes:GetObjectLoad(), 1)

        //call a method on the object, its non-static main
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeVirtual(), bytecode,
            "Main", "()V", false)

        //close out the main method
        methodWriter:VisitInstruction(opcodes:GetReturn())
        methodWriter:VisitMaxSize(2,2)
        methodWriter:VisitEnd()
    end

    action ComputeConstructor(boolean isParent)
        
        //integer numSystem = currentClass:getNumberSystemActions()
        text staticKey = clazz:GetStaticKey()
        text name = converter:ConvertStaticKeyToBytecodePath(staticKey)

        text signature = ""
        if isParent
            signature = "()V"
        else
            signature = "(Z)V"
        end


        //call the class's initialization function
        text null = undefined
        methodWriter = classWriter:VisitMethod(opcodes:GetPublic(), "<init>", signature, null, undefined)
        methodWriter:VisitCode()
        methodWriter:VisitVariable(opcodes:GetObjectLoad(), ME)
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeSpecial(), "java/lang/Object", "<init>", "()V", false)




        methodWriter:VisitInstruction(opcodes:GetReturn())
        methodWriter:VisitMaxSize(1, 1)
        methodWriter:VisitEnd()
    end

    action WriteJavaScript returns text
        constant text OUTPUT_VARIABLE = "OuTpUt_$tRiNg_"
        EOL = ""
        quote = EOL:GetDoubleQuote()
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        //quote = quote:GetDoubleQuote()
        convertedSource = "Main();" + EOL + "function Main() {" + EOL
        convertedSource = convertedSource + OUTPUT_VARIABLE + " = " + quote + quote + ";" + EOL

        Iterator<QuorumOpcode> fieldIterator = fields:GetIterator()
        repeat while fieldIterator:HasNext()
            QuorumOpcode field = fieldIterator:Next()
            convertedSource = convertedSource + field:WriteJavaScript()
        end


        Iterator<ActionOpcode> actionIterator = actions:GetIterator()
        repeat while actionIterator:HasNext()
            ActionOpcode act = actionIterator:Next()
            convertedSource = convertedSource + act:WriteJavaScript()
        end

        convertedSource = convertedSource + "return " + OUTPUT_VARIABLE + ";" + EOL + "}"

        return convertedSource
    end

    action GetTemplateCopy returns QuorumOpcode
        ClassOpcode op
        return op
    end
end