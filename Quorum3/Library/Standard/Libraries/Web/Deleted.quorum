package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The Deleted class represents HTML's (Hypertext Markup Language) del tag which is
used to show that text has been deleted from a document. This strikes through the 
deleted text. Citation of the text can be added, as well as, a time stamp of when 
the text was deleted.

You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_del.asp">The del attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      Paragraph p
      Deleted deleted
      Inserted inserted
      deleted:SetDescription("apples")
      inserted:SetDescription("bananas")
      p:SetDescription("I love " + deleted:Generate() + inserted:Generate())
      
      page:AddToBody(p)
      output page:Generate()
   end
end
*/
class Deleted is WebTag
    constant text cite = "cite"
    constant text timeStamp = "dateTime"

    /*
        This action sets the citation URL or web address.
        
        Attribute: Parameter value The citation URL or web address.

        Attribute: Example
        use Libraries.Web.Deleted

        Deleted deleted
        deleted:SetCitation("http://www.w3schools.com/tags/tag_del.asp")
        
    */
    action SetCitation(text value)
        AddAttribute(cite, value)
    end


    /*
        This action gets the citation URL or web address.
        

        Attribute: Returns The citation address.

        Attribute: Example
       use Libraries.Web.Deleted
       use Libraries.Web.Attribute

        Deleted deleted
        deleted:SetCitation("http://www.w3schools.com/tags/tag_del.asp")
        Attribute address = deleted:GetCitation()
        
    */
    action GetCitation returns Attribute
        return me:GetAttribute(cite)
    end

    /*
        This action sets the deleted time of the text.
        
        Attribute: Parameter value The date and time.

        Attribute: Example
       use Libraries.Web.Deleted

        Deleted deleted
        deleted:SetDeletedTime("2011-11-15T22:55:03Z")
        
    */
    action SetDeletedTime(text value)
        AddAttribute(timeStamp, value)
    end

    /*
        This action gets the set deleted time of the text.
        

        Attribute: Returns The date time of the deleted text.

        Attribute: Example
       use Libraries.Web.Deleted
       use Libraries.Web.Attribute

        Deleted deleted
        deleted:SetDeletedTime("2011-11-15T22:55:03Z")
        Attribute dateTime = deleted:GetDeletedTime()
        
    */
    action GetDeletedTime returns Attribute
        return me:GetAttribute(timeStamp)
    end

    action Generate returns text
        text result = "<del " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</del>"
        return result
    end
end