package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.all
use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator

class CreateObjectOpcode is QuorumOpcode
    //integer bytecodeLocation = -1
    //JavaBytecodeMethodWriter methodWriter = undefined
    JavaBytecodeOpcodes opcodes
    Class clazz = undefined
    Variable variable = undefined

    action GetVariable returns Variable
        return variable
    end

    action SetVariable(Variable field)
        me:variable = field
    end

    action GetClass returns Class
        return clazz
    end

    action SetClass(Class clazz)
        me:clazz = clazz
        Type type
        type:SetToObject(clazz)
    end

    action WriteJavaScript returns text
        EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        text result = "var " + variable:GetName() + " = new " + clazz:ConvertStaticKeyToUnderScore() + "();" + EOL
        return result
    end

    action Write
        if clazz not= undefined
            VisitLineNumber()
            JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
            if variable:IsField()
                //Class fieldHolder = variable:GetParentClass()
                methodWriter:VisitVariable(opcodes:GetObjectLoad(), 0)
                //methodWriter:VisitType(opcodes:GetCheckCast(), fieldHolder:ConvertStaticKeyToBytecodeInterfacePath())
            end
            methodWriter:VisitType(opcodes:GetNew(), clazz:ConvertStaticKeyToBytecodePath())
            methodWriter:VisitInstruction(opcodes:GetDuplicate())
            methodWriter:VisitMethodInvoke(opcodes:GetInvokeSpecial(), clazz:ConvertStaticKeyToBytecodePath(), "<init>", "()V", false)

            if variable:IsField()
                Class fieldHolder = variable:GetParentClass()
                path = fieldHolder:ConvertStaticKeyToBytecodePath()
                //path = clazz:ConvertStaticKeyToBytecodeInterfacePath()
                setterName = fieldHolder:ConvertToActionNameFromField(variable, false)
                Type type = variable:GetType()
                methodName = type:ConvertToSignatureFromFieldInterface(false)
                
                //cast this thing to the interface type
                //methodWriter:VisitType(opcodes:GetCheckCast(), clazz:ConvertStaticKeyToBytecodeInterfacePath())

                methodWriter:VisitMethodInvoke(opcodes:GetInvokeVirtual(), 
                    path, setterName, methodName, false)
            else
                methodWriter:VisitVariable(opcodes:GetObjectStore(), variable:GetBytecodeLocation() + GetBytecodeOffset())
            end
            
        end
    end

//    action GetMethodWriter returns JavaBytecodeMethodWriter
//        return methodWriter
//    end
//
//    action SetMethodWriter(JavaBytecodeMethodWriter writer)
//        methodWriter = writer
//    end

    action GetTemplateCopy returns QuorumOpcode
        CreateObjectOpcode con
        con:SetClass(GetClass())
        
        return con
    end
end