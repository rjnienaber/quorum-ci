package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Translate.BlockOpcode
use Libraries.Language.Compile.Location
use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator

class CheckDetectOpcode is QuorumOpcode
    BlockOpcode block = undefined
    DetectBlockOpcode alwaysBlock = undefined
    List<DetectBlockOpcode> detectBlocks
    JavaBytecodeOpcodes opcodes
    integer whichCheck = 0


    action AddDetectBlock(DetectBlockOpcode op)
        detectBlocks:Add(op)
    end

    action GetCheckLabelInteger returns integer
        return whichCheck
    end

    action SetCheckLabelInteger(integer value)
        whichCheck = value
    end

    action SetBlock(BlockOpcode block)
        me:block = block
    end

    action GetBlock returns BlockOpcode
        return block
    end

    action SetAlwaysBlock(DetectBlockOpcode block)
        me:alwaysBlock = block
    end

    action GetAlwaysBlock returns DetectBlockOpcode
        return alwaysBlock
    end

    action Write 
        JavaBytecodeLabel checkStart
        JavaBytecodeLabel alwaysStart
        JavaBytecodeLabel checkEnd
        JavaBytecodeLabel fullEnd

        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        text null = undefined //workaround
        if block not= undefined
            methodWriter:VisitLabel(checkStart)
            block:Write()
            methodWriter:VisitLabel(checkEnd)
            methodWriter:VisitJump(opcodes:GetGoto(), fullEnd)
            
        end

        Iterator<DetectBlockOpcode> it = detectBlocks:GetIterator()
        repeat while it:HasNext()
            JavaBytecodeLabel detectStart
            methodWriter:VisitLabel(detectStart)
            DetectBlockOpcode det = it:Next()
            det:Write()
            methodWriter:VisitTryCatchBlock(checkStart, checkEnd, detectStart, "java/lang/Exception")
        end

        if alwaysBlock not= undefined
            methodWriter:VisitLabel(alwaysStart)
            methodWriter:VisitVariable(opcodes:GetObjectStore(), 1)
            alwaysBlock:Write()
            methodWriter:VisitTryCatchBlock(checkStart, checkEnd, alwaysStart, "java/lang/Exception")
        end

        methodWriter:VisitLabel(fullEnd)
    end

    action WriteJavaScript returns text
        return ""
    end

    action GetTemplateCopy returns QuorumOpcode
        CheckDetectOpcode op


        return op
    end
end