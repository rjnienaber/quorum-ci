package Libraries.Language.Compile

use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator
use Libraries.Language.Compile.Location

class QualifiedName is Location
    List<text> names

    action Add(text name)
        names:Add(name)
    end

    action GetSize returns integer
        return names:GetSize()
    end

    action GetStaticKey returns text
        key = ""
        i = 0
        repeat GetSize() times
            key = key + names:Get(i)

            if i < GetSize() - 1
                key = key + "."
            end
            i = i + 1
        end

        return key
    end

    action GetPath returns text
        key = ""
        i = 0
        repeat GetSize() times
            key = key + names:Get(i)

            if i < GetSize() - 1
                key = key + "/"
            end
            i = i + 1
        end

        return key
    end

    action Empty
        names:Empty()
    end

    /*
        This action returns the full static key what a class would
        be given this qualified name and a name text field.
    */
    action GetClassStaticKey(text name) returns text
        text pack = me:GetStaticKey()
        if pack:IsEmpty()
            return name
        end
        return pack + "." + name
    end


    action Copy returns QualifiedName
        QualifiedName new
        Iterator<text> it = names:GetIterator()
        repeat while it:HasNext()
            text value = it:Next()
            new:Add(value)
        end
        return new
    end
end